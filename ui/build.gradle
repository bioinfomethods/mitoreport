plugins {
  id 'com.github.node-gradle.node' version '2.2.4'
}

description = 'Mito Report UI'

defaultTasks 'runTestUnit', 'runLint', 'runBuild'

node {
  version = '12.18.3'
  yarnVersion = '1.22.5'
  npmVersion = '6.14.8'
  download = true
}

FileTree uiFt = fileTree('.') {
  include '**/*'
  excludes = ['.*', 'node_modules', 'coverage', 'dist']
}

npm_install.configure {
  inputs.files('package.json', 'package-lock.json').withPropertyName('packageFiles')
  outputs.dir('node_modules').withPropertyName('nodeModules')
}

task runServe(type: NpmTask, dependsOn: 'npm_install') {
  group = 'development'
  description = 'npm run serve'
  args = ['run', 'serve']
}

task runBuild(type: NpmTask, dependsOn: 'npm_install') {
  group = 'build'
  description = 'npm run build'
  args = ['run', 'build']

  inputs.files(uiFt).withPropertyName('ui')
  outputs.dir('dist').withPropertyName('dist')
}

task runTestUnit(type: NpmTask, dependsOn: 'npm_install') {
  group = 'verification'
  description = 'npm run test:unit'
  args = ['run', 'test:unit']

  inputs.files(uiFt).withPropertyName('ui')
}

task runLint(type: NpmTask, dependsOn: 'npm_install') {
  group = 'verification'
  description = 'npm run lint'
  args = ['run', 'lint']

  inputs.files(uiFt).withPropertyName('ui')
}
