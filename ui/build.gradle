plugins {
  id 'com.github.node-gradle.node' version '3.3.0'
}

description = 'Mito Report UI'

defaultTasks 'runTestUnit', 'runLint', 'runBuild'

String vueAppBuildMode = System.getenv('VUE_APP_BUILD_MODE') ?: 'production'

node {
  download = Boolean.valueOf(property('nodeDownload'))
  version = "14.21.3"
}

task runServe(type: NpmTask, dependsOn: 'npmInstall') {
  group = 'development'
  description = 'npm run serve'
  args = ['run', 'serve']
}

task runBuild(type: NpmTask, dependsOn: 'npmInstall') {
  group = 'build'
  description = "npm run build --mode $vueAppBuildMode"
  args = ['run', 'build', '--mode', vueAppBuildMode]

  inputs.dir('.').withPropertyName('ui')
  outputs.dir('dist').withPropertyName('dist')
}

task runTestUnit(type: NpmTask, dependsOn: 'npmInstall') {
  group = 'verification'
  description = 'npm run test:unit'
  args = ['run', 'test:unit']
}

task runLint(type: NpmTask, dependsOn: 'npmInstall') {
  group = 'verification'
  description = 'npm run lint'
  args = ['run', 'lint']
}
